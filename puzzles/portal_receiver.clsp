; portal_receiver.clsp by yakuhito
;; used to receive messages via the portal

;; LAST_NONCE is here to make sure validator sigs for a 'chain' of nonce confirmations
;; are invalidated because the coin ids will be different (caused by different puzzle hashes)

(mod (
    VALIDATOR_INFO
    MESSAGE_COIN_MOD_HASH ; after 1st curry
    LAST_NONCE
    validator_sigs_switch
    new_puzzle_hash ; inner puzzle hash
    ; msg info
    nonce
    source_chain
    source_type
    source_info
    destination_type
    destination_info
    deadline
    message
  )

  (include condition_codes.clib)
  (include sha256tree.clib)
  (include validator_sigs.clib)
  (include curry.clib)

  (verify_validator_sigs
    VALIDATOR_INFO
    validator_sigs_switch
    (sha256tree (list
      new_puzzle_hash nonce source_chain source_type source_info destination_type destination_info deadline message
    ))
    (list
      (list CREATE_COIN new_puzzle_hash 1) ; upgrade mechanism
      (list CREATE_COIN (curry_hashes_inline MESSAGE_COIN_MOD_HASH
        (sha256 1 nonce)
        (sha256 2
          (sha256 1 source_info)
          (sha256 2
            (sha256 1 source_chain)
            (sha256 1 source_type)
          )
        )
        (sha256 2
          (sha256 1 destination_info)
          (sha256 1 destination_type)
        )
        (sha256 1 deadline)
        (sha256 1 (sha256tree message)) ; message was revealed on-chain
      ) 0)
    )
  )
)
